#include <ESP8266WiFi.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <SPI.h>
#include <MFRC522.h>

// Pins for RFID
#define SS_PIN D4     // SDA
#define RST_PIN D3    // RST

// Buzzer pin
#define BUZZER_PIN D8

MFRC522 rfid(SS_PIN, RST_PIN);
LiquidCrystal_I2C lcd(0x27, 16, 2);

int total = 0;

// RFID card UIDs
byte noodlesUID[4] = {0x15, 0xD4, 0x43, 0x1F};
byte appleUID[4]   = {0xB3, 0xBF, 0xA1, 0xA7};
byte drinksUID[4]  = {0xDB, 0x7B, 0x9F, 0x28};
byte totalUID[4]   = {0x13, 0xB5, 0x4A, 0x90};
byte chipsUID[4]   = {0x25, 0x58, 0x55, 0x1F}; // Replace with actual UID from DumpInfo

void setup() {
  Serial.begin(9600);
  SPI.begin();
  rfid.PCD_Init();

  Wire.begin(); // Initialize I2C for ESP8266
  lcd.begin(16, 2); // Initialize 16x2 LCD
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Scan Your Card");

  pinMode(BUZZER_PIN, OUTPUT);
  digitalWrite(BUZZER_PIN, LOW);
}

void loop() {
  if (!rfid.PICC_IsNewCardPresent() || !rfid.PICC_ReadCardSerial()) return;

  buzz(); // Notify card detected

  byte* uid = rfid.uid.uidByte;

  if (compareUID(uid, noodlesUID)) {
    addItem("Noodles", 35);
  } 
  else if (compareUID(uid, appleUID)) {
    addItem("Apple", 150);
  } 
  else if (compareUID(uid, drinksUID)) {
    addItem("Drinks", 200);
  } 
  else if (compareUID(uid, chipsUID)) {
    addItem("Chips", 50); // New item
  } 
  else if (compareUID(uid, totalUID)) {
    showTotal();
  } 
  else {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Unknown Card");
  }

  delay(2000); // Wait before next scan
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Scan Your Card");

  rfid.PICC_HaltA();
  rfid.PCD_StopCrypto1();
}

bool compareUID(byte *a, byte *b) {
  for (byte i = 0; i < 4; i++) {
    if (a[i] != b[i]) return false;
  }
  return true;
}

void addItem(String name, int price) {
  total += price;
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print(name + " Rs." + String(price));
  lcd.setCursor(0, 1);
  lcd.print("Added to Total");
  Serial.println(name + ": Rs. " + String(price));
}

void showTotal() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Total Amount:");
  lcd.setCursor(0, 1);
  lcd.print("Rs. " + String(total));
  Serial.println("TOTAL: Rs. " + String(total));
}

void buzz() {
  digitalWrite(BUZZER_PIN, HIGH);
  delay(200);
  digitalWrite(BUZZER_PIN,LOW);
}
